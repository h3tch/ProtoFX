// AUTHOR: Michael Hecher

/////////////
// BUFFERS //
/////////////

buffer buf_pos {
	usage staticDraw
	xml "geom/cube.xml" data/position
	xml "geom/plane.xml" data/position
}

buffer buf_col {
	usage staticDraw
	xml "geom/cube.xml" data/color
	xml "geom/plane.xml" data/color
}

buffer buf_idx {
	usage staticDraw
	xml "geom/cube.xml" data/index
	xml "geom/plane.xml" data/index
}

image img_shadowmap {
	width 512
	height 512
	format depth32f
}

/////////////////////
// BUFFER BINDINGS //
/////////////////////

vertinput in_vert {
	attr buf_pos float 4
	attr buf_col float 4
}

sampler samp_linear {
	minfilter linear
	magfilter linear
}

fragoutput out_shadowmap {
	// depth attachment
	depth img_shadowmap
}

texture tex_shadowmap {
	img img_shadowmap
}

/////////////
// SHADERS //
/////////////

shader vert vs_shadowmap {
	#version 440
	
	struct SpotLight {
		mat4 viewProj;
	};

	uniform SpotLight cs_light;
	
	// vertex input as specified by 'in_vert'
	layout(location = 0) in vec4 in_pos;
	
	void main ()
	{
		// transform vertex to light projection space
		gl_Position = cs_light.viewProj * in_pos;
	}
}

shader vert vs_render {
	#version 440
	
	struct SpotLight {
		mat4 viewProj;
	};

	struct SimpleCamera {
		mat4 viewProj;
	};

	uniform SpotLight cs_light;
	uniform SimpleCamera cs_camera;
	
	// vertex input as specified by 'in_vert'
	layout(location = 0) in vec4 in_pos;
	layout(location = 1) in vec4 in_col;

	// vertex shader output
	layout(location = 0) out vec4 out_lpos;
	layout(location = 1) out vec4 out_col;
	
	void main ()
	{
		// transform vertex to camera projection space
		gl_Position = cs_camera.viewProj * in_pos;
		// transform vertex to light projection space
		out_lpos = cs_light.viewProj * in_pos;
		// pass vertex color
		out_col = in_col;
	}
}

shader frag fs_render {
	#version 440
	#define NUM_POINTS 25
	#define PI 3.1415926535897932384626433832795

	struct SpotLight {
		mat4 viewProj;
		vec4 light;
	};
	
	uniform cs_poisson {
		vec2 points[NUM_POINTS];
	} disc;
	
	uniform SpotLight cs_light;
	
	layout(binding = 0) uniform sampler2D tex_shadowmap;
	
	layout(location = 0) in vec4 in_lpos;
	layout(location = 1) in vec4 in_col;
	out vec4 color;

	float rand(vec2 xy) {
		return fract(sin(dot(xy ,vec2(12.9898,78.233))) * 43758.5453);
	}

	mat2 randRot(vec2 xy) {
		float angle = 2 * PI * rand(xy);
		float sa = sin(angle);
		float ca = cos(angle);
		return mat2(ca, -sa, ca, sa);
	}
	
	void main ()
	{
		// pass color
		color = in_col;
		
		// if pixel outside light frustum clip space, do nothing
		if (any(greaterThan(abs(in_lpos), in_lpos.wwww)))
			return;
			
		// SHADOW MAPPING
		
		// transform to normalized device coordinates [-1;+1]
		// and then to shadow map texture coordinates [0;1]
		vec3 lpos = (in_lpos.xyz / in_lpos.w) * 0.5 + 0.5;
		mat2 R = randRot(lpos.xy);
		
		const vec2 r = 10.0 / textureSize(tex_shadowmap, 0);
		const float bias = 0.0005;

		float shadow = 0.0;
		for (int i = 0; i < NUM_POINTS; i++) {
			vec2 p = lpos.xy + R * disc.points[i] * r;
			shadow += lpos.z - bias > texture(tex_shadowmap, p).x ? 1.0 : 0.0;
		}
		
		if (shadow == 0.0)
			return;
		shadow /= float(NUM_POINTS);
		color.rgb *= (1.0 - shadow) * 0.5 + 0.5;
	}
}

/////////////
// PROGRAM //
/////////////

csharp cs_util {
	file "util/SpotLight.cs" "util/SimpleCamera.cs"
}

csharp cs_sampling {
	file "util/PoissonDisc.cs"
}

instance cs_light {
	class cs_util util.SpotLight
	pos 3 4 3
	rot -45 45 0
}

instance cs_camera {
	class cs_util util.SimpleCamera
	pos -5 4 10
	rot -20 -15 0
}

instance cs_poisson {
	class cs_sampling util.PoissonDisc
	maxSamples 64
	minRadius 0.3
}

pass pass_shadowmap {
	// attach shaders
	vert vs_shadowmap

	// render to
	fragout out_shadowmap
	
	// OpenGL calls
	Clear depthBit
	Enable depthTest
	Enable CullFace
	
	// draw cube
	draw buf_idx in_vert ushort triangles 0 0 36
	draw buf_idx in_vert ushort triangles 8 36 6
}

pass pass_render {
	// attach shaders
	vert vs_render
	frag fs_render
	
	// OpenGL calls
	ClearColor 0.1 0.3 1.0 0.0
	Clear colorDepthBit
	Enable depthTest
	
	// draw cube
	draw buf_idx in_vert ushort triangles 0 0 36
	draw buf_idx in_vert ushort triangles 8 36 6
}

pass pass_reset {
	ClearColor 0 0 0 0
	Disable depthTest
	Disable CullFace
}

tech tech_simple {
	pass pass_shadowmap
	pass pass_render
	pass pass_reset
}
// AUTHOR: Michael Hecher

// global variables
#global MAX_POISSON_POINTS 25

/////////////
// BUFFERS //
/////////////

buffer buf_pos {
	usage staticDraw
	xml "geom/cube.xml" data/position
	xml "geom/plane.xml" data/position
}

buffer buf_col {
	usage staticDraw
	xml "geom/cube.xml" data/color
	xml "geom/plane.xml" data/color
}

buffer buf_idx {
	usage staticDraw
	xml "geom/cube.xml" data/index
	xml "geom/plane.xml" data/index
}

image img_shadowmap {
	size 512 512
	format depth32f
}

/////////////////////
// BUFFER BINDINGS //
/////////////////////

vertinput in_vert {
	attr buf_pos float 4
	attr buf_col float 4
}

sampler samp_linear {
	minfilter linear
	magfilter linear
}

fragoutput out_shadowmap {
	// depth attachment
	depth img_shadowmap
}

texture tex_shadowmap {
	img img_shadowmap
}

/////////////
// SHADERS //
/////////////

shader vert vs_shadowmap {
	#version 440
	
	layout(binding = 0) uniform SpotLight {
		mat4 viewProj;
	} light;
	
	// vertex input as specified by 'in_vert'
	layout(location = 0) in vec4 in_pos;
	
	void main ()
	{
		// transform vertex to light projection space
		<<gl_Position>> = <<light.viewProj>> * <<in_pos>>;
	}
}

shader vert vs_render {
	#version 440
	
	layout(binding = 0) uniform SpotLight {
		mat4 viewProj;
		vec4 camera;
		vec4 light;
	} light;
	
	layout(binding = 1) uniform SimpleCamera {
		mat4 viewProj;
	} camera;
	
	// vertex input as specified by 'in_vert'
	layout(location = 0) in vec4 in_pos;
	layout(location = 1) in vec4 in_col;

	// vertex shader output
	layout(location = 0) out vec4 out_lpos;
	layout(location = 1) out vec4 out_col;
	
	void main ()
	{
		// transform vertex to camera projection space
		<<gl_Position>> = camera.viewProj * in_pos;
		// transform vertex to light projection space
		<<out_lpos>> = light.viewProj * in_pos;
		// pass vertex color
		out_col = in_col;
	}
}

shader frag fs_render {
	#version 440
	#define PI 3.1415926535897932384626433832795
	
	layout(binding = 0) uniform SpotLight {
		mat4 viewProj;
		vec4 camera;
		vec4 light;
	} light;

	layout(binding = 2) uniform PoissonDisc {
		vec2 points[MAX_POISSON_POINTS];
		int numPoints;
	} disc;
	
	layout(binding = 0) uniform sampler2D shadowmap;
	
	layout(location = 0) in vec4 in_lpos;
	layout(location = 1) in vec4 in_col;
	out vec4 color;

	const float lightR = light.light.y;
	const float zNear = light.camera.z;
	const float zFar = light.camera.w;
	const vec2 fovVec = light.light.zw;
	
	float rand(vec2 xy) {
		return fract(sin(dot(xy ,vec2(12.9898,78.233))) * 43758.5453);
	}

	mat2 randRot(float angle) {
		float sa = sin(angle);
		float ca = cos(angle);
		return mat2(ca, -sa, ca, sa);
	}
	
	vec2 depthNormalBias(vec3 lpos) {
		vec3 normal = cross(dFdx(lpos), dFdy(lpos));
		return vec2(1,-1) * normal.xy / normal.z;
	}

	float light2plane(float value, float zReceiver) {
		return value * (zReceiver - zNear) / zReceiver;
	}

	vec2 plane2tex(float value) {
		return vec2(value) / fovVec;
	}

	float light2penumbra(float value, float zBlocker, float zReceiver) {
		return value * (zReceiver - zBlocker) / zBlocker;
	}

	float penumbra2plane(float value, float zReceiver) {
		return value * zNear / zReceiver;
	}
	
	float bias(vec2 zChange, vec2 offset) {
		return dot(zChange, offset) - 0.001;
	}
	
	float exp2linDepth(float z) {
		return zFar * zNear / (zFar - z * (zFar - zNear));  
	}

	float BlockerSearch(vec2 filterR, vec3 lpos, vec2 surfBias) {
		int nBlocker = 0;
		float zBlocker = 0.0;
		mat2 R = randRot(2 * PI * rand(lpos.xy));

		for (int i = 0; i < disc.numPoints; i++) {
			vec2 p = R * disc.points[i] * filterR;
			float z = texture(shadowmap, lpos.xy + p).x;
			//z = exp2linDepth(z);
			if (lpos.z + bias(surfBias, p) > z) {
				zBlocker += z;
				nBlocker++;
			}
		}
		//return zBlocker / float(nBlocker);
		return exp2linDepth(zBlocker / float(nBlocker));
	}

	float PCF(vec2 filterR, vec3 lpos, vec2 surfBias) {
		// PCF SHADOW MAPPING
		float shadow = 0.0;
		mat2 R = randRot(2 * PI * rand(lpos.xy));

		for (int i = 0; i < disc.numPoints; i++) {
			vec2 p = R * disc.points[i] * filterR;
			float z = texture(shadowmap, lpos.xy + p).x;
			//z = exp2linDepth(z);
			shadow += lpos.z + bias(surfBias, p) > z ? 1.0 : 0.0;
		}

		return shadow / float(disc.numPoints);
	}

	float PCSS(vec2 surfBias, vec3 lpos) {
		float receiverZ = exp2linDepth(lpos.z);
		
		// BLOCKER SEARCH
		float planeR = light2plane(lightR, receiverZ);
		vec2 filterR = plane2tex(planeR);
		float zBlocker = BlockerSearch(filterR, lpos, surfBias);
		if (isnan(zBlocker))
			return 0;//zBlocker = lpos.z;

		// PCF SHADOW MAPPING
		float penumbraR = light2penumbra(lightR, zBlocker, receiverZ);
		planeR = penumbra2plane(penumbraR, receiverZ);
		filterR = plane2tex(planeR);
		return PCF(filterR, lpos, surfBias);
	}
	
	void main () {
		// pass color
		<<color>> = in_col;
		
		//vec3 lpos = (in_lpos.xyz / in_lpos.w) * 0.5 + 0.5;
		//color.rgb = normalize(cross(dFdx(lpos),dFdy(lpos)));
		//return;
		
		// if pixel outside light frustum clip space, do nothing
		if (any(greaterThan(abs(in_lpos), in_lpos.wwww)))
			return;
			
		// SHADOW MAPPING
		
		// transform to normalized device coordinates [-1;+1]
		// and then to shadow map texture coordinates [0;1]
		vec3 lpos = (in_lpos.xyz / in_lpos.w) * 0.5 + 0.5;
		
		vec3 normal = cross(dFdx(lpos),dFdy(lpos));
		if (normal.z < 0) {
			return;
		}
			
		vec2 surfBias = -normal.xy / normal.z;
		
		//const vec2 r = 10.0 / textureSize(shadowmap, 0);
		//float shadow = PCF((r.x + r.y) * 0.5, lpos, zChange);
		float <<shadow>> = PCSS(surfBias, lpos);
			
		if (shadow == 0.0)
			return;
		
		color.rgb *= (1.0 - shadow) * 0.5 + 0.5;
	}
}

/////////////
// PROGRAM //
/////////////

csharp cs_util {
	file "<csharp>/StaticCamera.cs" ...
		 "<csharp>/SimpleCamera.cs" ...
		 "<csharp>/SpotLight.cs" ...
	     "<csharp>/PoissonDisc.cs" ...
		 "<csharp>/UniformBlock.cs" ...
	     "<csharp>/CsObject.cs"
}

instance cs_light {
	class cs_util csharp.SpotLight
	name SpotLight
	pos 3 4 3
	rot -45 45 0
	near 2
	far 40
	radius 1
}

instance cs_camera {
	class cs_util csharp.SimpleCamera
	name SimpleCamera
	pos -2.5 2 5
	rot -20 -15 0
}

instance cs_poisson {
	class cs_util csharp.PoissonDisc
	name PoissonDisc
	maxPoints MAX_POISSON_POINTS
	minRadius 0.2
}

pass pass_shadowmap {
	// attach shaders
	vert vs_shadowmap

	// render to
	fragout out_shadowmap
	
	// OpenGL calls
	glClear depthBit
	glEnable depthTest
	glEnable CullFace

	// execute camera code
	exec cs_light
	
	// draw cube
	draw buf_idx in_vert ushort triangles 0 0 36
	draw buf_idx in_vert ushort triangles 8 36 6
}

pass pass_render {
	// attach shaders
	vert vs_render
	frag fs_render
	
	tex tex_shadowmap 0
	
	// OpenGL calls
	glClearColor 0.1 0.3 1.0 0.0
	glClear colorDepthBit
	glEnable depthTest

	// execute camera code
	exec cs_light
	exec cs_camera
	exec cs_poisson
	
	// draw cube
	draw buf_idx in_vert ushort triangles 0 0 36
	draw buf_idx in_vert ushort triangles 8 36 6
}

pass pass_reset {
	tex 0
	glClearColor 0 0 0 0
	glDisable depthTest
	glDisable CullFace
}

tech tech_simple {
	pass pass_shadowmap
	pass pass_render
	pass pass_reset
}